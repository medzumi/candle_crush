title: Bristle_0
tags: 
colorID: 0
position: 267,1328
---
<<CanMove false>>
<<StringCustomCommand PlaySound Bristle_10>>
Bristle : ...
Lighty : ...
<<StringCustomCommand PlaySound Bristle_1>>
Bristle : Is this how the world looked like before?...
<<StringCustomCommand PlaySound Bristle_6>>
Bristle : I ... Imagined it differently. Less.. White, I think.
Lighty : ...
<<StringCustomCommand PlaySound Bristle_10>>
Bristle : Now that there is light... I think it’s time for me to move on. I’ve been waiting for this. 
<<StringCustomCommand PlaySound Bristle_8>>
Bristle : Would you help me with that? You seem nice enough. 
Lighty : ...
<<StringCustomCommand PlaySound Bristle_1>>
Bristle : I’ll take that as a reluctant yes. 
<<StringCustomCommand PlaySound Bristle_8>>
Bristle : There must be canvases in my yard. I don’t know where they are exactly… But could you please fetch them and pile them up? 
<<StringCustomCommand PlaySound Bristle_8>>
Bristle : That’ll make everything easier. Please. 
<<set $Bristle to 1>>
<<CanMove true>>
===
title: Piles
tags: 
colorID: 0
position: -442,1326
---
<<CanMove false>>
<<if $Piles is 0>>
    <<jump Piles_0>>
<<endif>>
<<if $Piles is 1>>
    <<jump Piles_1>>
<<endif>>
<<if $Piles is 2>>
    <<jump Piles_2>>
<<endif>>
<<if $Piles is 3>>
    <<jump Piles_3>>
<<endif>>
<<if $Piles is 4>>
    <<jump Piles_4>> 
<<endif>>
<<CanMove true>>
===
title: Star_Start
tags: 
colorID: 0
position: 1193,1301
---
<<CanMove false>>
<<if $Star_Start is 0>>
<<set $Star_Start to 1>>
Star : Oh my God! 
Star : Who am I seeing!
Star : What a bright personality!
Star : Could I be honored to know your name?
Lighty : ...
<<StringCustomCommand PlaySound Roberto_2>>
Star : What a humble one!
Star : That’s even better!
Star : Oh well, allow me to introduce myself, even though you must already know who I am.
Star : ROBERTO STARS 
Star : Now, let me give you an autograph. 
Lighty : ...
<<StringCustomCommand PlaySound Roberto_8>>
Star : No? 
<<StringCustomCommand PlaySound Roberto_2>>
Star : Ha-ha 
<<StringCustomCommand PlaySound Roberto_6>>
Star : Oh c’mon...
Lighty : ...
<<StringCustomCommand PlaySound Roberto_8>>
Star : Pretty please? 
Lighty : ...
Star : ha 
Star : ha ha 
Star : AHAHAHAHAHAHA 
Star : DAMN WHAT AM I SAYING?! 
Star : WHO THE HECK NEEDS THOSE DARNED AUTOGRAPHS IN THIS DARKNESS? 
Lighty : ...
<<StringCustomCommand PlaySound Roberto_4>>
Star : I used to have everything!
<<StringCustomCommand PlaySound Roberto_5>>
Star : EVERYTHING! 
Star : Do you understand? 
Star : Money 
Star : Fame 
Star : Recognition
Star : Peoples’ love 
Star : And now?
Star : And now everyone understands that I’m a fake!
<<StringCustomCommand PlaySound Roberto_3>>
Star : A star must shine no matter what! 
<<StringCustomCommand PlaySound Roberto_6>>
Star : And that’s not something I could do...
Lighty :...
<<StringCustomCommand PlaySound Roberto_10>>
Star : I can not even light the way...
Lighty : ...
<<StringCustomCommand PlaySound Roberto_1>>
Star : ... But YOU can! You will be my guiding star! I dream of going to the mountains but I need equipment… Where would one find hiking equipment?
Lighty : ...
<<StringCustomCommand PlaySound Roberto_7>>
Star : Hmm? Oh right! In the mountains! You are right! 
Lighty : ...
<<endif>>

<<if $Star_Start is 1>>
    <<if $Hiking_Equipment is 1>>
        <<set $Star_Start to 2>>
        <<StringCustomCommand PlaySound Roberto_7>>
        Star : Ahh it’s so bright. Oh, it was you, my star
        Star : How is it going? 
        Lighty : ...
        <<StringCustomCommand PlaySound Roberto_8>>
        Star : You found it? 
        <<StringCustomCommand PlaySound Roberto_9>>
        Star : I knew you could do it! 
        <<RemoveItemWithCount Hiking_Equipment 1>>
        <<StringCustomCommand PlaySound QuestDone>>
        <<jump Star_2>>
    <<else>>
        <<StringCustomCommand PlaySound Roberto_9>>
        Star : I will be waiting here, find some hiking equipment, it should be somewhere in the mountains. 
    <<endif>>
<<endif>>

<<if $Star_Start is 2>>
    <<jump Star_2>>
<<endif>>
<<CanMove true>>
===
title: Star_2
tags: 
colorID: 0
position: 1429,1309
---
<<CanMove false>>
<<StringCustomCommand PlaySound Roberto_3>>
Star : So, meet you at the top of the mountain?
<<CanMove true>>
===
title: Star_On_Mountain
tags: 
colorID: 0
position: 1200,1521
---
<<CanMove false>>
<<if $Star_On_Mountain is false>>
<<set $Star_On_Mountain to true>>
<<StringCustomCommand PlaySound Roberto_6>>
Star : PAAANT-PAAANT-PAAANT. I DID IT!
Lighty : ...
Star : WE did it 
Star : ... 
<<StringCustomCommand PlaySound Roberto_1>>
Star : Huh, so this is what they are like...
Star : the real stars. 
<<StringCustomCommand PlaySound Roberto_10>>
Star : Not to offend you, of course. 
<<StringCustomCommand PlaySound Roberto_10>>
Star : You will always be my guiding star. 
Star : And everyone wanted me to take this place, you know. 
Star : And I didn't mind that either. 
Lighty : ...
<<StringCustomCommand PlaySound Roberto_6>>
Star : But over time everyone understood that all I do is play my role. 
<<StringCustomCommand PlaySound Roberto_3>>
Star : What else can I do? 
Star : I’m just an actor. 
Star : Unlike you...
Star : Your cause is just. 
Lighty : ...
<<StringCustomCommand PlaySound Roberto_7>>
Star : I would never be able to climb here by myself. 
<<IncreasePlayerLighting 2>>
<<StringCustomCommand PlayComics Star>>
<<StringCustomCommand PlaySound QuestDone>>
<<endif>>
Star : Thank you.
<<CanMove true>>
===
title: Sasha_2
tags: 
colorID: 0
position: 720,1764
---
<<CanMove false>>
<<StringCustomCommand PlaySound Sasha_9>>
Sasha : You still there? Sorry. I haven't gotten used to those glasses yet.
<<CanMove true>>
===
title: Sasha_0
tags: 
colorID: 0
position: 720,1327
---
<<CanMove false>>
<<StringCustomCommand PlaySound Sasha_2>>
Sasha : Wait a second here. Is that a new face I’m seeing? 
<<StringCustomCommand PlaySound Sasha_6>>
Sasha : If you can call that flame of yours a face, that is. 
Lighty : ...
<<StringCustomCommand PlaySound Sasha_10>>
Sasha : Whichever it is, good. One more second in this emptiness and I’d start playing chess with myself.
<<StringCustomCommand PlaySound Sasha_3>>
Sasha : If my eyes do not deceive me, you can see a thing or two in this world. While you’re at it, will you fetch one for me? 
<<StringCustomCommand PlaySound Sasha_9>>
Sasha : It’s in a box with a cross drawn on it. Inside is a pair of sunglasses. 
<<StringCustomCommand PlaySound Sasha_6>>
Sasha : Many would - and do, in fact - think that’s a piece of junk in our current state of living, but not me. I’ll make use of it, you just do your thing. So, you on it? 
Lighty : ...
<<StringCustomCommand PlaySound Sasha_7>>
Sasha : That’s what I thought! Now go, before you burn my eyes down.
<<set $Sasha to 1>>
<<jump Sasha_1>>
<<CanMove true>>
===
title: Sasha_1
tags: 
colorID: 0
position: 720,1543
---
<<CanMove false>>
<<if $Box_With_Cross is 1>>
    <<StringCustomCommand PlaySound Sasha_7>>
    Sasha : Wow, you actually managed to find it! And it’s not even a random box you drew a cross on. Good job.
    <<StringCustomCommand PlaySound Sasha_13>>
    Sasha : Now just let me try these on real quick. 
    Lighty : ...
    Lighty : ...
    Lighty : ...
    <<StringCustomCommand PlaySound Sasha_9>>
    Sasha : So. This is what it’s like, huh. 
    <<StringCustomCommand PlaySound Sasha_10>>
    Sasha : I... If my eyesight was like this, I wouldn’t be as cheerful, too. 
    <<StringCustomCommand PlaySound Sasha_3>>
    Sasha : In case you never knew that about a cat or never saw one, cats see well in the dark, that’s what they do.
    <<StringCustomCommand PlaySound Sasha_9>>
    Sasha : And that might be one thing to be jealous of in this world. 
    <<StringCustomCommand PlaySound Sasha_10>>
    Sasha : It certainly does keep one entertained but not for an eternity. So if I change my approach I just might make new connections with others and stay sane. 
    Lighty : ...
    <<StringCustomCommand PlaySound Sasha_5>>
    Sasha : Huh. I’ll have to think about this for a moment. 
    <<StringCustomCommand PlaySound Sasha_9>>
    Sasha : You go. You have a bright personality, you know. Too bright for my eyes at times, even. So go. 
    Lighty : ...
    <<RemoveItem Box_With_Cross>>
    <<IncreasePlayerLighting 2>>
    <<set $Sasha to 2>>
    <<StringCustomCommand PlaySound QuestDone>>
<<else>>
    <<StringCustomCommand PlaySound Sasha_6>>
    Sasha : Return once you’ve found the box with a cross drawn on it.    
<<endif>>
<<CanMove true>>
===
title: Sasha
tags: 
colorID: 0
position: 491,1332
---
<<CanMove false>>
<<if $Sasha is 0>>
    <<jump Sasha_0>>
<<endif>>
<<if $Sasha is 1>>
    <<jump Sasha_1>>
<<endif>>
<<if $Sasha is 2>>
    <<jump Sasha_2>>
<<endif>>
<<CanMove true>>
===
title: Bristle_5
tags: 
colorID: 0
position: 267,2413
---
<<CanMove false>>
<<StringCustomCommand PlaySound Bristle_9>>
Bristle : Thank you. You gave me strength.
<<CanMove true>>
===
title: MountainWall
tags: 
colorID: 0
position: 1200,1738
---
<<if $Star_Start is 2>>
    <<BoolCustomCommand StarOnMountain true>>
    <<BoolCustomCommand StarOnCastle false>>
<<endif>>
===
title: CheckFinishNode
tags: 
colorID: 0
position: 1199,1095
---
<<if $Bristle is 5 && $Sasha is 2 && $Star_On_Mountain is true>>
    <<BoolCustomCommand TurnTower true>>
<<endif>>
===
title: Bristle
tags: 
colorID: 0
position: 7,1326
---
<<CanMove false>>
<<if $Bristle is 0>>
    <<jump Bristle_0>>
<<endif>>
<<if $Bristle is 1>>
    <<jump Bristle_1>>
<<endif>>
<<if $Bristle is 2>>
    <<jump Bristle_2>>
<<endif>>
<<if $Bristle is 3>>
    <<jump Bristle_3>>
<<endif>>
<<if $Bristle is 4>>
    <<jump Bristle_4>>
<<endif>>
<<if $Bristle is 5>>
    <<jump Bristle_5>>
<<endif>>
<<CanMove true>>
===
title: Bristle_1
tags: 
colorID: 0
position: 269,1553
---
<<if $Canvases_On_Yard_0 is 1 && $Canvases_On_Yard_1 is 1 && $Canvases_On_Yard_2 is 1 && $Canvases_On_Yard_3 is 1>>
    <<StringCustomCommand PlaySound Bristle_8>>
    Bristle : Wait, you actually did that? I didn’t think of a proper gratitude...
    <<StringCustomCommand PlaySound Bristle_9>>
    <<set $Bristle to 2>>
    <<RemoveItemWithCount Canvases_On_Yard_0 1>>
    <<RemoveItemWithCount Canvases_On_Yard_1 1>>
    <<RemoveItemWithCount Canvases_On_Yard_2 1>>
    <<RemoveItemWithCount Canvases_On_Yard_3 1>>
    <<StringCustomCommand PlaySound QuestDone>>
    <<BoolCustomCommand TurnPiles true>>
    Bristle : Thank you, I guess. 
    Lighty : ...
    <<StringCustomCommand PlaySound Bristle_2>>
    Bristle : Now, one more thing to ask you. 
    <<StringCustomCommand PlaySound Bristle_2>>
    Bristle : Just go near the pile and act carefree.. Like get super near it. And flicker your fire a little bit.
    <<StringCustomCommand PlaySound Bristle_6>>
    Bristle : I don’t know what I had drawn while in the dark. I imagined lots of nature and people I love and the night sky. But is it really what’s on the canvases? 
    Lighty : ...
    <<jump Bristle_2>>
<<else>>
<<StringCustomCommand PlaySound Bristle_2>>
Bristle : Gather my canvas for me, Lighty.
<<endif>>
<<CanMove true>>
===
title: Bristle_3
tags: 
colorID: 0
position: 268,1977
---
<<StringCustomCommand PlaySound Bristle_10>>
Bristle: Okay, now go burn the pile!
<<CanMove true>>
===
title: Bristle_2
tags: 
colorID: 0
position: 272,1767
---
<<StringCustomCommand PlaySound Bristle_3>>
Bristle :Okay, what I’m saying is burn the pile. Please.
<<CanMove true>>
===
title: Bristle_4
tags: 
colorID: 0
position: 271,2197
---
<<StringCustomCommand PlaySound Bristle_10>>
Bristle : Burn it now! Now for sure.
<<CanMove true>>
===
title: Piles_0
tags: 
colorID: 0
position: -220,1326
---
<<StringCustomCommand PlaySound Bristle_4>>
Bristle : Wait, no! It’s not burning yet, is it? I want to stand near it for a little while longer. 
Bristle : ...
Bristle : ...
Bristle : ...
<<set $Bristle to 3>>
<<set $Piles to 1>>
<<jump Bristle_3>>
<<CanMove true>>
===
title: Piles_1
tags: 
colorID: 0
position: -221,1549
---
<<StringCustomCommand PlaySound Bristle_1>>
Bristle : Wait wait wait wait wait.
<<StringCustomCommand PlaySound Bristle_4>>
Bristle : I said that out of politeness. I didn't want to keep you waiting. I need more time. 
Bristle : ...
Bristle : ...
Bristle : ...
<<set $Bristle to 4>>
<<set $Piles to 2>>
<<jump Bristle_4>>
<<CanMove true>>
===
title: Piles_2
tags: 
colorID: 0
position: -214,1767
---
<<StringCustomCommand PlaySound Bristle_5>>
<<BoolCustomCommand TurnPilesTrigger false>>
Bristle : STOP!
Lighty : ...
<<StringCustomCommand PlaySound Bristle_1>>
Bristle : Okay sorry. Forget what I asked.
<<StringCustomCommand PlaySound Bristle_6>>
Bristle : You don’t need to do that. I don’t want it.
Lighty : ...
<<StringCustomCommand PlaySound Bristle_10>>
Bristle : It will be far better if I face what I did. It’s scary. 
<<StringCustomCommand PlaySound Bristle_6>>
Bristle : It will be different from what I imagined but it’s what I did. For better or for worse. Now go. 
<<IncreasePlayerLighting 2>>
<<StringCustomCommand PlaySound QuestDone>>
<<set $Bristle to 5>>
<<jump Bristle_5>>
<<CanMove true>>
===
title: FinishNode
tags: 
colorID: 0
position: 1198,874
---
<<CanMove false>>
<<StringCustomCommand PlayComics FinalComics>>
===
